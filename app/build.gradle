apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.github.triplet.play'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

ext{
    supportLibVersion = "26.1.0"
    multiDexVersion = "1.0.2"
}

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.2'
    defaultConfig {
        applicationId "com.mydroid"
        minSdkVersion 16
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }


    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            debuggable false
            zipAlignEnabled true
            // signingConfig signingConfigs.release
            //proguardFiles 'proguard-android-optimize.txt', getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            debuggable true
            minifyEnabled false
            applicationIdSuffix ".debug"
            testCoverageEnabled true
        }
    }

    sourceSets {
        main {
            java.srcDirs += 'src/main/kotlin'
            res.srcDirs = [
                    "src/main/res/layouts/feature_main",
                    "src/main/res/layouts/feature_mydroid",
                    "src/main/res/layouts/feature_body_parts",
                    "src/main/res/layouts/feature_master_list",
                    "src/main/res/layouts/common",
                    "src/main/res/layouts",
                    "src/main/res",
            ]
        }

        androidTest {
            java.srcDirs += "src/androidTest/kotlin"
        }

        test {
            java.srcDirs += "src/test/kotlin"
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
        unitTests.all {
            maxParallelForks = 2
            if (System.getenv().containsKey("CIRCLE_BUILD_NUM")) {
                //in CI, we have limited memory, this will allow us to refresh the VM from any memory leaks.
                forkEvery 5
            }
        }
    }

    dexOptions {
        preDexLibraries = false
        javaMaxHeapSize "4g"
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "com.android.support:appcompat-v7:$supportLibVersion"
    implementation "com.android.support:multidex:$multiDexVersion"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"

    // Dagger
    compileOnly "javax.annotation:jsr250-api:$javaxAnnotationVersion"
    implementation "com.google.dagger:dagger:$daggerVersion"
    implementation "javax.inject:javax.inject:$javaxInjectVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"

    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testImplementation 'junit:junit:4.12'
}
